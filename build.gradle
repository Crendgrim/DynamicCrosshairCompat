plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'com.modrinth.minotaur' version '2.+'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/" }
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url = "https://minecraft.guntram.de/maven/"
	}
	maven { url 'https://maven.wispforest.io' }
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
	flatDir {
		dir "lib"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

	modImplementation include(fabricApi.module("fabric-api-base", project.fabric_api_version))
	modImplementation include(fabricApi.module("fabric-key-binding-api-v1", project.fabric_api_version))
	modCompileOnly include(fabricApi.module("fabric-transfer-api-v1", project.fabric_api_version))
	modCompileOnly include(fabricApi.module("fabric-screen-handler-api-v1", project.fabric_api_version))
	modCompileOnly("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}")
	modCompileOnly("de.guntram.mcmod:GBfabrictools:${project.gbfabrictools_version}")
	modCompileOnly("io.wispforest:owo-lib:${project.owo_version}")
	modCompileOnly("software.bernie.geckolib:geckolib-fabric-${project.gecko_version}")

	/* Dependencies for DynamicCrosshair, so it runs in a dev environment */
	if (project.hasProperty("dynamiccrosshair_use_local_jar")) {
		modImplementation(name: "dynamiccrosshair")
	} else {
		modImplementation("maven.modrinth:dynamiccrosshair:${project.dynamiccrosshair_version}")
	}
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	/* Supported Mods */
	def modFile=file("supported_mods.csv")
	modFile.splitEachLine(",") { fields ->
		switch (fields[1]) {
			case "curse":
				modCompileOnly("curse.maven:${fields[0]}-${fields[2]}") { transitive = false }
				break;
			case "modrinth":
				modCompileOnly("maven.modrinth:${fields[0]}:${fields[2]}") { transitive = false }
				break;
		}
	}
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand(project.properties)
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = project.modrinth_id
	uploadFile = remapJar
	versionType = project.version_type
	changelog = project.changelog
	gameVersions = List.of(project.supported_versions.split(';'))
	loaders = ['fabric']
	dependencies {
		required.project project.dynamiccrosshair_modrinth_id
	}
}

curseforge {
	apiKey = "${System.getenv("CURSEFORGE_TOKEN")}"
	project {
		id = project.curseforge_id
		changelog = project.changelog
		releaseType = project.version_type
		addGameVersion "Fabric"
		project.supported_versions.split(';').each {
			addGameVersion it
		}

		mainArtifact(remapJar) {
			displayName = "${project.mod_name} ${project.version}"
			relations {
				requiredDependency "dynamic-crosshair"
			}
		}
	}
	options {
		forgeGradleIntegration = false
	}
}
