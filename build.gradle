plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'com.modrinth.minotaur' version '2.+'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/" }
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

	modCompileOnly include(fabricApi.module("fabric-api-base", project.fabric_api_version))
	modCompileOnly include(fabricApi.module("fabric-key-binding-api-v1", project.fabric_api_version))

	/* Dependencies for DynamicCrosshair, so it runs in a dev environment */
	modImplementation("maven.modrinth:dynamiccrosshair:${project.dynamiccrosshair_version}") { transitive = false }
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	/* Supported Mods */
	modCompileOnly("maven.modrinth:arcanus:${project.arcanus_version}") { transitive = false }
	modCompileOnly("curse.maven:bclib-${project.bclib_version}") { transitive = false }
	modCompileOnly("curse.maven:betternether-${project.betternether_version}") { transitive = false }
	modCompileOnly("curse.maven:betterend-${project.betterend_version}") { transitive = false }
}

processResources {
	filesMatching("fabric.mod.json") {
		expand(project.properties)
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = project.modrinth_id
	uploadFile = remapJar
	versionType = project.version_type
	changelog = project.changelog
	gameVersions = List.of(project.supported_versions.split(';'))
	loaders = ['fabric']
	dependencies {
		required.project 'EwPIWPJ9' // Dynamic Crosshair
	}
}

curseforge {
	apiKey = "${System.getenv("CURSEFORGE_TOKEN")}"
	project {
		id = project.curseforge_id
		changelog = project.changelog
		releaseType = project.version_type
		addGameVersion "Fabric"
		project.supported_versions.split(';').each {
			addGameVersion it
		}

		mainArtifact(remapJar) {
			displayName = "${project.mod_name} ${project.version}"
			relations {
				requiredDependency "dynamic-crosshair"
			}
		}
	}
	options {
		forgeGradleIntegration = false
	}
}
